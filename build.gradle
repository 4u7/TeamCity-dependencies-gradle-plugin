plugins{
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'org.jetbrains.kotlin.jvm' version '1.2.70'
}

group = 'com.github.jk1'
description = 'TeamCity dependencies plugin resolves project dependencies via local TeamCity server'
version = '0.19'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.2.70'

    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile gradleTestKit()
    testCompile "org.hamcrest:hamcrest-core:1.3" // Hamcrest matchers
    testRuntime "cglib:cglib-nodep:3.1"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.1"    // allows mocking of classes without default constructor
}

// compilation ordering modified to allow joint compilation for both Kotlin and Groovy
compileGroovy.dependsOn = compileGroovy.taskDependencies.getMutableValues() - 'compileJava'
compileKotlin.dependsOn compileGroovy
compileKotlin.classpath += files(compileGroovy.destinationDir)
classes.dependsOn compileKotlin

test {
    outputs.upToDateWhen { false }
    testLogging {
        afterSuite { desc, r ->
            if (!desc.parent) { // will match the outermost suite
                println "${r.testCount} tests, ${r.successfulTestCount} successes, " +
                        "${r.failedTestCount} failures, ${r.skippedTestCount} skipped"
            }
        }
    }
}

// release publishing to gradle plugin repo
pluginBundle {
    website = 'https://github.com/jk1/TeamCity-dependencies-gradle-plugin'
    vcsUrl = 'https://github.com/jk1/TeamCity-dependencies-gradle-plugin'
    description = project.description
    tags = ['dependency-management', 'teamcity']
    plugins {
        teamcityDependenciesPlugin {
            id = 'com.github.jk1.tcdeps'
            displayName = 'Gradle plugin to fetch dependencies from JetBrains TeamCity'
        }
    }
}
